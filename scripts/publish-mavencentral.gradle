task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.source
}

artifacts {
    archives androidSourcesJar
}

apply plugin: 'maven'
apply plugin: 'signing'

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    println "Found secret props file, loading props"
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        ext[name] = value
    }
} else {
    println "No props file, loading env vars"
    ext["signing.keyId"] = System.getenv('SIGNING_KEY_ID')
    ext["signing.password"] = System.getenv('SIGNING_PASSWORD')
    ext["signing.secretKeyRingFile"] = System.getenv('SIGNING_SECRET_KEY_RING_FILE')
    ext["ossrhUsername"] = System.getenv('OSSRH_USERNAME')
    ext["ossrhPassword"] = System.getenv('OSSRH_PASSWORD')
}

nexusStaging {
    packageGroup = 'hu.autsoft'
    stagingProfileId = 'caceeedcae039c'
    username = ossrhUsername
    password = ossrhPassword
}

uploadArchives {
    archivesBaseName = PUBLISH_ARTIFACT_ID

    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

//            repository(url: mavenLocal().url)

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name PUBLISH_ARTIFACT_ID
                packaging 'aar'
                description 'A Kotlin SharedPreferences wrapper'
                url 'https://github.com/autsoft/krate'

                scm {
                    connection 'scm:git:github.com/autsoft/krate.git'
                    developerConnection 'scm:git:ssh://github.com/autsoft/krate.git'
                    url 'https://github.com/autsoft/krate/tree/master'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'autsoft-android-dev'
                        name 'AutSoft Android'
                        email 'android-dev@autsoft.hu'
                    }
                }
            }
        }
    }
}

signing {
    sign configurations.archives
}
