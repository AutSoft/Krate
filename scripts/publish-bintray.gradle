apply plugin: 'maven-publish'
apply plugin: "com.jfrog.bintray"

bintray {
    dryRun = false
    publish = true
    override = false

    Properties properties = new Properties()

    def localPropsFile = project.rootProject.file('local.properties')
    if (localPropsFile.exists()) {
        properties.load(localPropsFile.newDataInputStream())
    }

    user = properties.getProperty('bintrayUser') ?: System.getenv('BINTRAY_USER')
    key = properties.getProperty('bintrayKey') ?: System.getenv('BINTRAY_API_KEY')

    publications('release')

    pkg {
        repo = BINTRAY_REPO
        name = BINTRAY_NAME
        vcsUrl = BINTRAY_VCS_URL

        publicDownloadNumbers = true
        licenses = ["Apache-2.0"]
        version {
            name = PUBLISH_VERSION
            vcsTag = PUBLISH_VERSION
            released = new Date()
        }
    }

    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION
                artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
                artifact androidSourcesJar

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    project.configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}
